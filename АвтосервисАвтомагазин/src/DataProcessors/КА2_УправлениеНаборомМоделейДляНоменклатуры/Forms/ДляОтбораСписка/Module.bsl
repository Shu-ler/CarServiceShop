// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	СчетчикИспользования = СчетчикИспользования + 1;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Объект.КлючиМоделей.Очистить();

	МассивКлючиМоделей = Настройки.Получить("КлючиМоделей");

	Если МассивКлючиМоделей <> Неопределено Тогда
		Для Каждого Стр Из МассивКлючиМоделей Цикл
			НовСтр = Объект.КлючиМоделей.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)

	МассивПустыеПоля = ПараметрыСеанса.КА2_КлючиМоделейПустыеПоля;

	ДляСтруктуры = Новый Массив;
	РазделительПолей = ", ";

	Для Каждого Элемент Из МассивПустыеПоля Цикл
		ДляСтруктуры.Добавить(Элемент.ИмяПоля);
	КонецЦикла;

	КлючиСтруктуры = СтрСоединить(ДляСтруктуры, РазделительПолей);

	МассивКлючиМоделей = Новый Массив;

	Для Каждого Стр Из Объект.КлючиМоделей Цикл
		СтрСтр = Новый Структура(КлючиСтруктуры);
		ЗаполнитьЗначенияСвойств(СтрСтр, Стр);
		МассивКлючиМоделей.Добавить(СтрСтр);
	КонецЦикла;

	Настройки.Вставить("КлючиМоделей", МассивКлючиМоделей);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлючиМоделей

&НаКлиенте
Процедура ПоколениеКузовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.КлючиМоделей.ТекущиеДанные;

	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("Марка", ТекущиеДанные.МаркаНаименование);
	ПараметрыВызова.Вставить("Модель", ТекущиеДанные.МодельНаименование);

	ОткрытьФорму("Справочник.КА2_КлючиМоделей.Форма.ФормаВыбора",
				 ПараметрыВызова,
				 Элементы.ПоколениеКузов,
				 ЭтаФорма.УникальныйИдентификатор,
				 ,
				 ,
				 ,
				 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПоколениеКузовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ЗаполнитьЗначенияСвойств(Элементы.КлючиМоделей.ТекущиеДанные, ВыбранноеЗначение);

	Элемент = ВыбранноеЗначение.ПоколениеКузов;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьИУстановитьОтбор(Команда)

	МассивОтбора = КА2_КаталогАвтомобилейВызовСервера.МассивОтбора(Объект.КлючиМоделей, РасширенныйРежим = 1);
	СтрокаВНаименование = КА2_КаталогАвтомобилейКлиентСервер.СформироватьСтрокуВНаименование(Объект.КлючиМоделей);

	ЭтотОбъект.ВладелецФормы.АвтоЗаголовок = Ложь;
	ЭтотОбъект.ВладелецФормы.Заголовок = "Номенклатура - для " + СтрокаВНаименование;

	КА2_ФильтрКлиентСервер.УстановитьОтборПоМоделиАвтомобиля(ЭтотОбъект.ВладелецФормы, МассивОтбора);

	ЭтотОбъект.Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБезУстановкиОтбора(Команда)

	ИмяКомандыОчистки = "ОчиститьТЧ";

	Если Команда.Имя = ИмяКомандыОчистки Тогда
		Объект.КлючиМоделей.Очистить();
	КонецЕсли;

	КА2_ФильтрКлиентСервер.ОчиститьОтборПоМоделиАвтомобиля(ЭтотОбъект.ВладелецФормы);
	ЭтотОбъект.Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура КлючиМоделейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Совместимо = Истина;
	КонецЕсли;

КонецПроцедуры
#КонецОбласти