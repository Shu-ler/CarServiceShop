// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает количество записей в регистре КА2_КлючиМоделейОбновить
// 
// Возвращаемое значение:
// 	Число - Количество записей
Функция КоличествоЗаписей() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	КОЛИЧЕСТВО(КА2_КлючиМоделейОбновить.КлючМодели) КАК КлючМоделиКоличество
				   |ИЗ
				   |	РегистрСведений.КА2_КлючиМоделейОбновить КАК КА2_КлючиМоделейОбновить";

	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();

	Возврат РезультатЗапроса.КлючМоделиКоличество;

КонецФункции

// Очищает регистр КА2_КлючиМоделейОбновить и заносит в него ссылки всех элементов справочника КА2_КлючиМоделей,
// не помеченных на удаление
// 
// Возвращаемое значение:
// 	Число - Количество записей в регистре после обновления
Функция ВсеКлючиПодготовить() Экспорт

	// Создание выборки из всех элементов справочника КА2_КлючиМоделей, не помеченных на удаление 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	КА2_КлючиМоделей.Ссылка
				   |ИЗ
				   |	Справочник.КА2_КлючиМоделей КАК КА2_КлючиМоделей
				   |ГДЕ
				   |	НЕ КА2_КлючиМоделей.ПометкаУдаления";
	НаОбновление = Запрос.Выполнить().Выбрать();

	// Создание и очистка набора записей
	КлючиМоделейНаборЗаписей = РегистрыСведений.КА2_КлючиМоделейОбновить.СоздатьНаборЗаписей();
	КлючиМоделейНаборЗаписей.Записать(Истина);

	// Занесение всех элементов выборки НаОбновление в набор записей регистра
	Пока НаОбновление.Следующий() Цикл
		НоваяЗапись = КлючиМоделейНаборЗаписей.Добавить();
		НоваяЗапись.КлючМодели = НаОбновление.Ссылка;
	КонецЦикла;

	КлючейНаОбновление = КлючиМоделейНаборЗаписей.Количество();

	// Запись набора
	Если КлючейНаОбновление > 0 Тогда
		КлючиМоделейНаборЗаписей.Записать(Истина);
	КонецЕсли;

	Возврат КлючейНаОбновление;

КонецФункции

#КонецОбласти

#КонецЕсли