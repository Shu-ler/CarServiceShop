// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Область ПрограммныйИнтерфейс

Процедура ДобавитьКомандуДобавитьМодели(ТаблицаКоманд, НастройкиФормы) Экспорт

	КомандаДобавитьМоделиАвтомобилей(ТаблицаКоманд, "ДИФ_КонтекстноеМеню");
	КомандаДобавитьМоделиАвтомобилей(ТаблицаКоманд, "ДИФ_Еще");

КонецПроцедуры


Процедура ДобавитьКомандуОтборПоКаталогуАвтомобилей(ТаблицаКоманд, НастройкиФормы) Экспорт

	КомандаОтбора = ТаблицаКоманд.Добавить();
	КомандаОтбора.Вид = "ОтборПоКаталогуАвтомобилей";
	КомандаОтбора.Представление = НСтр("ru= 'Отбор по модели автомобиля'");
	КомандаОтбора.Обработчик = "КА2_ПодключаемыеКомандыКлиент.Подключаемый_ОткрытьФормуОтборПоКаталогуАвтомобилей";
	КомандаОтбора.ИмяФормы = "ФормаСписка";
	КомандаОтбора.РежимЗаписи = "НеЗаписывать";
	КомандаОтбора.МножественныйВыбор = Истина;
	КомандаОтбора.ИзменяетВыбранныеОбъекты = Ложь;
	КомандаОтбора.Порядок = 6;
	КомандаОтбора.Важность = "Обычное";
	КомандаОтбора.Картинка = БиблиотекаКартинок.АС2_ФильтрПоАвто;

КонецПроцедуры

Процедура Подключаемый_НеБСП_КомандыОтбораПоКаталогуАвтомобилей(Форма) Экспорт

	ИмяОбработчикаНаФорме = "ОткрытьОтборПоКаталогуАвтомобилей";
	ГруппаФильтр = Форма.Элементы[ПараметрыСеанса.КА2_ИмяГруппыФильтра];

	// Команда отбора
	КомандаОтбора = Форма.Команды.Добавить(ИмяОбработчикаНаФорме);
	КомандаОтбора.Действие = ИмяОбработчикаНаФорме;
	КомандаОтбора.Заголовок = НСтр("ru= 'Отбор по модели автомобиля'");

	// Кнопка отбора
	ИмяКнопки = "КнопкаОтбораПоКаталогуАвтомобилей";

	КнопкаОтбора = Форма.Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), ГруппаФильтр);
	КнопкаОтбора.ИмяКоманды = КомандаОтбора.Имя;
	КнопкаОтбора.Картинка = БиблиотекаКартинок.АС2_ФильтрПоАвто;

КонецПроцедуры

Процедура Подключаемый_НеБСП_КомандыОчисткиОтбораПоКаталогуАвтомобилей(Форма) Экспорт

	ЗаголовокИПодсказка = НСтр("ru= 'Отменить отбор по модели автомобиля'");
	ИмяОбработчикаОчисткиНаФорме = "ОтменитьОтборПоКаталогуАвтомобилей";
	ИмяКнопки = "КнопкаОчисткиОтбораПоКаталогуАвтомобилей";
	ИмяГруппыФильтр = ПараметрыСеанса.КА2_ИмяГруппыФильтра;
	
	// Команда отключения отбора
	КомандаОчистки = Форма.Команды.Добавить(ИмяОбработчикаОчисткиНаФорме);
	КомандаОчистки.Действие = ИмяОбработчикаОчисткиНаФорме;
	КомандаОчистки.Заголовок = ЗаголовокИПодсказка;
	КомандаОчистки.Подсказка = ЗаголовокИПодсказка;

	// Кнопка
	СледующийЭлемент = Неопределено;

	ГруппаФильтр = Форма.Элементы.Найти(ИмяГруппыФильтр);

	Если ГруппаФильтр <> Неопределено Тогда

		КоманднаяПанель = ГруппаФильтр.Родитель;

		Если КоманднаяПанель <> Неопределено Тогда

			// Создание кнопки	
			КнопкаОчистки = Форма.Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), КоманднаяПанель);
			КнопкаОчистки.ИмяКоманды = КомандаОчистки.Имя;
			КнопкаОчистки.Отображение = ОтображениеКнопки.Картинка;
			КнопкаОчистки.Картинка = БиблиотекаКартинок.АС2_ФильтрПоАвтоОчистка;
			КнопкаОчистки.Доступность = Ложь;

			// Перемещение кнопки 
			СледующийЭлемент = НайтиСледующийЭлемент(ГруппаФильтр);

			Если СледующийЭлемент <> Неопределено И СледующийЭлемент <> КнопкаОчистки Тогда
				Форма.Элементы.Переместить(КнопкаОчистки, КоманднаяПанель, СледующийЭлемент);
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура КомандаДобавитьМоделиАвтомобилей(ТаблицаКоманд, Вид)

	Команда1 = ТаблицаКоманд.Добавить();
	Команда1.Вид = Вид;
	Команда1.Представление = НСтр("ru= 'Добавить модели автомобилей'");
	Команда1.Обработчик = "КА2_ПодключаемыеКомандыКлиент.Подключаемый_ОткрытьФормуДобавитьМоделейАвтомобилей";
	Команда1.ИмяФормы = "ФормаСписка";
	Команда1.РежимЗаписи = "Записывать";
	Команда1.МножественныйВыбор = Истина;
	Команда1.ИзменяетВыбранныеОбъекты = Истина;
	Команда1.Порядок = 96;
	Команда1.Важность = "Обычное";
	Команда1.Картинка = БиблиотекаКартинок.АС2_ФильтрПоАвто;
	
КонецПроцедуры

Функция НайтиСледующийЭлемент(ГруппаФильтр)

	СледующийЭлемент = Неопределено;
	Найден = Ложь;

	КоманднаяПанель = ГруппаФильтр.Родитель;

	Для Каждого Элемент Из КоманднаяПанель.ПодчиненныеЭлементы Цикл
	
		Если Найден Тогда
			СледующийЭлемент = Элемент;
			Прервать;
		КонецЕсли;

		Если Элемент = ГруппаФильтр Тогда
			Найден = Истина;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат СледующийЭлемент;
	
КонецФункции

#КонецОбласти