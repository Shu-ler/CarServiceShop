// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Область ПрограммныйИнтерфейс

Процедура УстановитьОтборПоМоделиАвтомобиля(Знач Форма, Знач МассивОтбора) Экспорт

	ИмяКнопкиОчисткиОтбора = "КнопкаОчисткиОтбораПоКаталогуАвтомобилей";
	ГруппаОтбораМаркаМодель = ГруппаОтбораМаркаМодель(Форма);

	Для Каждого Эл Из МассивОтбора Цикл
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораМаркаМодель,
															   "Ссылка.[ТекстПоискаМодели]",
															   ВидСравненияКомпоновкиДанных.Содержит,
															   Эл,
															   ,
															   Истина);
	КонецЦикла;
	
	ИспользоватьОтбор = ЗначениеЗаполнено(МассивОтбора);

	Форма.Элементы[ИмяКнопкиОчисткиОтбора].Доступность = ИспользоватьОтбор;
	
	ГруппаОтбораМаркаМодель.Использование = ИспользоватьОтбор;
	
	Форма.ОбновитьОтображениеДанных();
	
КонецПроцедуры

Процедура ОчиститьОтборПоМоделиАвтомобиля(Знач Форма) Экспорт

	ИмяКнопкиОчисткиОтбора = "КнопкаОчисткиОтбораПоКаталогуАвтомобилей";

	ГруппаОтбораМаркаМодель = ГруппаОтбораМаркаМодель(Форма);
	ГруппаОтбораМаркаМодель.Использование = Ложь;
	
	Форма.Элементы[ИмяКнопкиОчисткиОтбора].Доступность = Ложь;

	Форма.Заголовок = "";
	Форма.АвтоЗаголовок = Истина;
	
	Форма.ОбновитьОтображениеДанных();

КонецПроцедуры

Функция ГруппаОтбораМаркаМодель(Знач Форма) Экспорт

	ИмяСписка = "СписокНоменклатура";
	ИмяГруппыОтбора = "ОтборПоМаркеИМодели";

	СписокОтбор = Форма[ИмяСписка].Отбор;

	ГруппаОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(СписокОтбор, , ИмяГруппыОтбора);

	Если ЗначениеЗаполнено(ГруппаОтбора) Тогда
		ГруппаОтбораМаркаМодель = ГруппаОтбора[0];
		ГруппаОтбораМаркаМодель.Элементы.Очистить();
	Иначе
		ГруппаОтбораМаркаМодель = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(СписокОтбор,
																							ИмяГруппыОтбора,
																							ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	КонецЕсли;

	Возврат ГруппаОтбораМаркаМодель

КонецФункции

#КонецОбласти