// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Область ПрограммныйИнтерфейс

// Возвращает строку моделей автомомбилей по содержимому таблицы ТаблицаДанных
// 
// Параметры:
// 	ТаблицаДанных - ТаблицаЗначений, ДанныеФормыКоллекция - Таблица ключей моделей 
// Возвращаемое значение:
// 	Строка - Строка вида "Volvo XC70 (2008-2011) / Audi A4 Универсал (2006-2009)"
Функция СформироватьСтрокуВНаименование(ТаблицаДанных) Экспорт

	СНН = Новый Массив;
	ПрефиксМарки = "";

	ОтборПоСтрокам = Новый Структура("Совместимо", Истина);

	// Формирование коллекции марок
	НаборМарок = КоллекцияРазных(ТаблицаДанных, "МаркаНаименование", ОтборПоСтрокам);

	Если НаборМарок = Неопределено Тогда
		Возврат "";
	КонецЕсли;

	Для Каждого Марка Из НаборМарок Цикл

		ПрефиксМодели = " ";
		СНН.Добавить(ПрефиксМарки);
		СНН.Добавить(Строка(Марка));

		ОтборПоСтрокам.Вставить("МаркаНаименование", Марка);
	
		// Формирование коллекции моделей для марки
		НаборМоделей = КоллекцияРазных(ТаблицаДанных, "МодельНаименование", ОтборПоСтрокам);

		Для Каждого Модель Из НаборМоделей Цикл

			ОтборПоСтрокам.Вставить("МодельНаименование", Модель);

			// Формирование коллекции кузовов для моделей
			НаборКузовов = КоллекцияРазных(ТаблицаДанных, "Кузов", ОтборПоСтрокам, Истина);

			Для Каждого Кузов Из НаборКузовов Цикл

				ОтборПоСтрокам.Вставить("КузовНаименование", Кузов);

				СНН.Добавить(ПрефиксМодели);
				СНН.Добавить(Строка(Модель));

				Если Не ПустаяСтрока(Кузов) Тогда
					СНН.Добавить(" ");
					СНН.Добавить(Кузов);
				КонецЕсли;

				// Формирование коллекции поколений для модели 
				ОтборСтрокКлючейМоделей = ТаблицаДанных.НайтиСтроки(ОтборПоСтрокам);

				ГодыВыпуска = ГодыВыпускаДляНесколькихСтрок(ОтборСтрокКлючейМоделей);

				Если СтрДлина(ГодыВыпуска) > 3 Тогда
					СНН.Добавить(ГодыВыпуска);
				КонецЕсли;

				ОтборПоСтрокам.Удалить("КузовНаименование");
				ПрефиксМодели = ", ";

			КонецЦикла;

			ОтборПоСтрокам.Удалить("МодельНаименование");

			ПрефиксМодели = ", ";
		КонецЦикла;

		ОтборПоСтрокам.Удалить("МаркаНаименование");

		ПрефиксМарки = " / ";

	КонецЦикла;

	Возврат СтрСоединить(СНН);

КонецФункции

// Возвращает годы производства строкой вида "2002-2012" или "(2002-2012)"
// 
// Параметры:
// 	ГодНП - Строка - Год начала производства
// 	ГодОП - Строка - Год окончания производства
// 	ВСкобках - Булево - Окружить годы выпуска скобками
// 	ПробелВНачале - Булево - Добавить пробел перед годами выпуска
// Возвращаемое значение:
// 	Строка - Годы выпуска
Функция ГодыВыпускаСтрока(ГодНП, ГодОП, ВСкобках = Ложь, ПробелВНачале = Ложь) Экспорт

	ШаблонГодов = ?(ВСкобках, "%1(%2%3%4)", "%1%2%3%4");

	Тире = ?(ПустаяСтрока(ГодНП) Или ПустаяСтрока(ГодОП), "", "-");
	Префикс = ?(ПробелВНачале, " ", "");

	ГодыВыпуска = СтрШаблон(ШаблонГодов, Префикс, ГодНП, Тире, ГодОП);

	Возврат ГодыВыпуска;

КонецФункции

// Конструктор структуры общих настроек каталога автомобилей
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * ТекстПоискаМодели - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - Свойство "ТекстПоискаМодели" 
// * ОтборПоКаталогуАвтомобилей - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - Свойство "Текст отбора по моделям";
// * МаркиМодели - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - Свойство "Марки, модели" 
Функция НастройкиКаталогаАвтомобилей() Экспорт
	Возврат Новый Структура("ТекстПоискаМодели, ОтборПоКаталогуАвтомобилей, МаркиМодели")
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает массив разных ссылок или строк из таблицы ТаблицаДанных для колонки Колонка
// 
// Параметры:
// 	ТаблицаДанных - ТаблицаЗначений, ДанныеФормыКоллекция - Таблица для отбора 
// 	Колонка - Строка - Наименование колонки поиска
// 	Отбор - Структура - Структура отбора
// 	ПоСтроке - Булево - Поиск по строке (Истина) или по ссылке (Ложь)
// Возвращаемое значение:
// 	Массив, Неопределено - Массив строк или ссылок
Функция КоллекцияРазных(ТаблицаДанных, Колонка, Отбор, ПоСтроке = Ложь)

	СтрокиСОбъектами = ТаблицаДанных.НайтиСтроки(Отбор);

	НаборОбъектов = Новый Массив;

	Для Каждого СтрокаМоделей Из СтрокиСОбъектами Цикл

		ОбъектПоиска = ?(ПоСтроке, Строка(СтрокаМоделей[Колонка]), СтрокаМоделей[Колонка]);

		Если НаборОбъектов.Найти(ОбъектПоиска) = Неопределено Тогда
			НаборОбъектов.Добавить(ОбъектПоиска);
		КонецЕсли;
	КонецЦикла;

	Возврат ?(НаборОбъектов.Количество() > 0, НаборОбъектов, Неопределено);

КонецФункции

// Формирует строку представления годов выпуска для набора строк
// 
// Параметры:
// 	НаборСтрок - ТаблицаЗначений, ДанныеФормыКоллекция - Набор строк ключей моделей автомобиля 
// Возвращаемое значение:
// 	Строка - Строка вида " (2001-2012)" или " (2001)" 
Функция ГодыВыпускаДляНесколькихСтрок(Знач НаборСтрок)

	ГодНПМакс = "2200";
	ГодНП = ГодНПМакс;

	ГодОПМин = "1900";
	ГодОП = ГодОПМин;

	Для Каждого СтрокаИзНабора Из НаборСтрок Цикл

		СтрокаИзНабораГодНП = Строка(СтрокаИзНабора.ГодНП);
		СтрокаИзНабораГодОП = Строка(СтрокаИзНабора.ГодОП);

		Если Не ПустаяСтрока(СтрокаИзНабораГодНП) И СтрокаИзНабораГодНП < ГодНП Тогда
			ГодНП = СтрокаИзНабораГодНП;
		КонецЕсли;

		Если Не ПустаяСтрока(СтрокаИзНабораГодОП) И СтрокаИзНабораГодОП > ГодОП Тогда
			ГодОП = СтрокаИзНабораГодОП;
		КонецЕсли;

	КонецЦикла;

	ГодНП = СтрЗаменить(ГодНП, ГодНПМакс, "");
	ГодОП = СтрЗаменить(ГодОП, ГодОПМин, "");

	Возврат ГодыВыпускаСтрока(ГодНП, ГодОП, Истина, Истина);

КонецФункции

#КонецОбласти