// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет справочник данными из файла поставщика
// 
// Параметры:
// 	ИмяФайла - Строка - Полное имя файла
// Возвращаемое значение:
// 	Структура - Описание:
// * ИмяСправочника - Строка - Наименование справочника
// * Загружено - Число - Количество обновленных элементов справочника
// * Ошибок - Число - Количество ошибок обновления
Функция Обновить(ИмяФайла) Экспорт

	ИмяСправочника = Метаданные.Справочники.КА2_Кузова.Имя;

	РезультатОбновления = КА2_КаталогАвтомобилейВызовСервера.ОбновитьСправочник(ИмяСправочника, ИмяФайла);

	Возврат РезультатОбновления;

КонецФункции

// Возвращает выборку элементов справочника с пустым полем date_create
// 
// Возвращаемое значение:
// 	ВыборкаИзРезультатаЗапроса - Описание
Функция Некорректные() Экспорт

	Возврат КА2_КаталогАвтомобилейВызовСервера.КоллекцияНекорректных(Метаданные.Справочники.КА2_Кузова.Имя);

КонецФункции

// Возвращает коллекцию кузовов по наименованию марки и наименованию модели
// 
// Параметры:
// 	Марка - СправочникСсылка.КА2_НаименованияМарок - Наименование марки
// 	Модель - СправочникСсылка.КА2_НаименованияМоделей - Наименование модели 
// Возвращаемое значение:
// 	Массив из СправочникСсылка.КА2_Кузова - Массив ссылок на отобранные элементы справочника КА2_Кузова
Функция НайтиПоНаименованиямМаркиИМодели(Марка, Модель) Экспорт

	Если ТипЗнч(Марка) <> Тип("СправочникСсылка.КА2_НаименованияМарок")
		 Или ТипЗнч(Модель) <> Тип("СправочникСсылка.КА2_НаименованияМоделей") Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	КА2_Кузова.Ссылка
						  |ИЗ
						  |	Справочник.КА2_Кузова КАК КА2_Кузова
						  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КА2_Модели КАК КА2_Модели
						  |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КА2_Марки КАК КА2_Марки
						  |			ПО КА2_Модели.id_car_mark = КА2_Марки.Код
						  |		ПО КА2_Кузова.Владелец = КА2_Модели.Ссылка
						  |ГДЕ
						  |	КА2_Марки.Владелец = &Марка
						  |	И КА2_Модели.Владелец = &Модель
						  |УПОРЯДОЧИТЬ ПО
						  |	КА2_Кузова.Наименование,
						  |	КА2_Кузова.id_car_type");
	Запрос.УстановитьПараметр("Марка", Марка);
	Запрос.УстановитьПараметр("Модель", Модель);

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

КонецФункции

Функция ДополнительныеДанные(Ссылка) Экспорт

	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Наименование, Владелец");

	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("КузовНаименование", Реквизиты.Наименование);
	
	Если ТипЗнч(Реквизиты.Владелец) <> Тип("СправочникСсылка.КА2_Поколения") Тогда
	
	ДополнительныеДанные.Вставить("ТипТС", Неопределено);
	ДополнительныеДанные.Вставить("Марка", Неопределено);
	ДополнительныеДанные.Вставить("Модель", Реквизиты.Владелец);

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				   |	КА2_Типы.Ссылка КАК ТипТС,
				   |	КА2_Марки.Ссылка КАК Марка
				   |ИЗ
				   |	Справочник.КА2_Модели КАК КА2_Модели
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КА2_Марки КАК КА2_Марки
				   |		ПО КА2_Марки.Код = КА2_Модели.id_car_mark
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КА2_Типы КАК КА2_Типы
				   |		ПО КА2_Типы.Код = КА2_Модели.id_car_type
				   |ГДЕ
				   |	КА2_Модели.Ссылка = &Модель";
	Запрос.УстановитьПараметр("Модель", Реквизиты.Владелец);

	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Количество() = 1 Тогда
		Результат.Следующий();
		ДополнительныеДанные.Вставить("ТипТС", Результат.ТипТС);
		ДополнительныеДанные.Вставить("Марка", Результат.Марка);
	КонецЕсли;

	КонецЕсли;
	
	Возврат ДополнительныеДанные;

КонецФункции

#КонецОбласти

#КонецЕсли