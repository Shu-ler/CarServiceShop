// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДляУправлениеНаборомМоделейДляНоменклатуры = Параметры.Свойство("УправлениеНаборомМоделейДляНоменклатуры");

	Если ДляУправлениеНаборомМоделейДляНоменклатуры Тогда

		СтандартнаяОбработка = Ложь;

		НаименованиеМарки = Неопределено;
		НаименованиеМодели = Неопределено;
		Поколение = Неопределено;

		Параметры.Отбор.Свойство("Ссылка", НаименованиеМарки);
		Параметры.Отбор.Свойство("Наименование", НаименованиеМодели);
		Параметры.Отбор.Свойство("Владелец", Поколение);

		Если Поколение = Справочники.КА2_Поколения.ПустаяСсылка()
			 И ЗначениеЗаполнено(НаименованиеМарки)
			 И ЗначениеЗаполнено(НаименованиеМодели) Тогда

			ИмяПоля = "Ссылка";
			ВидСравненияДляОтбора = ВидСравненияКомпоновкиДанных.ВСписке;
			ПравоеЗначение = Справочники.КА2_Кузова.НайтиПоНаименованиямМаркиИМодели(НаименованиеМарки,
																					 НаименованиеМодели);

		Иначе

			ИмяПоля = "Владелец";
			ВидСравненияДляОтбора = ВидСравненияКомпоновкиДанных.Равно;
			ПравоеЗначение = Поколение;

			Элементы.НаименованиеТипа.Видимость = Ложь;

		КонецЕсли;

		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																				ИмяПоля,
																				ПравоеЗначение,
																				ВидСравненияДляОтбора);
	КонецЕсли;

	// Кеширование текста запроса динамического списка
	КешТекстаЗапроса = Список.ТекстЗапроса;
	
	// Отбор только по записям из файлов последней поставки
	ТолькоИзБазыПоставщика = Истина;
	ТолькоИзБазыПоставщикаУстановитьОтбор();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТолькоИзБазыПоставщикаПриИзменении(Элемент)

	ТолькоИзБазыПоставщикаУстановитьОтбор();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличнойЧастиСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кузова", Строки.ПолучитьКлючи());
	Запрос.Текст = "ВЫБРАТЬ
				   |	КА2_Кузова.Ссылка КАК Ссылка,
				   |	КОЛИЧЕСТВО(КА2_КлючиМоделей.Ссылка) КАК Вхождений
				   |ИЗ
				   |	Справочник.КА2_КлючиМоделей КАК КА2_КлючиМоделей
				   |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КА2_Кузова КАК КА2_Кузова
				   |		ПО КА2_КлючиМоделей.Кузов = КА2_Кузова.Ссылка
				   |ГДЕ
				   |	КА2_КлючиМоделей.Кузов В (&Кузова)
				   |СГРУППИРОВАТЬ ПО
				   |	КА2_Кузова.Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();

	КА2_КаталогАвтомобилейВызовСервера.УстановитьВыделениеСтрок(Строки, Выборка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТолькоИзБазыПоставщикаУстановитьОтбор()

	Если ТолькоИзБазыПоставщика Тогда
		КешТекстаЗапроса = Список.ТекстЗапроса;
		ЗапросДопОтбор = " И НЕ КА2_Кузова.ПометкаУдаления И КА2_Кузова.date_create <> """"";
		Список.ТекстЗапроса = КешТекстаЗапроса + ЗапросДопОтбор;
	Иначе
		Список.ТекстЗапроса = КешТекстаЗапроса;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти