// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает коллекцию моделей по наименованию марки
// 
// Параметры:
// 	Марка - СправочникСсылка.КА2_НаименованияМарок - Наименование марки
// 	ТолькоИмеющиесяВКлючах - Булево - Вариант отбора
// Возвращаемое значение:
// 	Массив - см.СправочникСсылка.КА2_Модели
Функция НайтиПоНаименованиюМарки(Марка, ТолькоИмеющиесяВКлючах = Ложь, ТолькоИзБазыПоставщика = Ложь) Экспорт

	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	КА2_Модели.Владелец КАК МодельНаименованиеСсылка,
				   |	КА2_Модели.Владелец.Наименование КАК МодельНаименованиеСтрока
				   |ИЗ
				   |	Справочник.КА2_Модели КАК КА2_Модели
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КА2_Марки КАК КА2_Марки
				   |		ПО КА2_Модели.id_car_mark = КА2_Марки.Код
				   |ГДЕ
				   |	КА2_Марки.Владелец = &МаркаНаименование
				   |УПОРЯДОЧИТЬ ПО
				   |	МодельНаименованиеСтрока";

	ТекстЗапросаТолькоИзБазыПоставщика = "ВЫБРАТЬ РАЗЛИЧНЫЕ
										 |	КА2_Модели.Владелец КАК МодельНаименованиеСсылка,
										 |	КА2_Модели.Владелец.Наименование КАК МодельНаименованиеСтрока,
										 |	КА2_Модели.Код,
										 |	КА2_Модели.date_create
										 |ИЗ
										 |	Справочник.КА2_Модели КАК КА2_Модели
										 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КА2_Марки КАК КА2_Марки
										 |		ПО КА2_Модели.id_car_mark = КА2_Марки.Код
										 |ГДЕ
										 |	КА2_Марки.Владелец = &МаркаНаименование
										 |	И КА2_Модели.date_create <> """"
										 |УПОРЯДОЧИТЬ ПО
										 |	МодельНаименованиеСтрока";

	ТекстЗапросаПоКлючам = "ВЫБРАТЬ РАЗЛИЧНЫЕ
						   |	КА2_КлючиМоделей.Модель.Владелец КАК МодельНаименованиеСсылка,
						   |	КА2_КлючиМоделей.Модель.Владелец.Наименование КАК МодельНаименованиеСтрока
						   |ИЗ
						   |	Справочник.КА2_КлючиМоделей КАК КА2_КлючиМоделей
						   |ГДЕ
						   |	КА2_КлючиМоделей.Марка.Владелец = &МаркаНаименование
						   |	И НЕ КА2_КлючиМоделей.ПометкаУдаления
						   |УПОРЯДОЧИТЬ ПО
						   |	МодельНаименованиеСтрока";

	Запрос = Новый Запрос;
	Запрос.Текст = ?(ТолькоИмеющиесяВКлючах, ТекстЗапросаПоКлючам, ?(ТолькоИзБазыПоставщика,
																	 ТекстЗапросаТолькоИзБазыПоставщика,
																	 ТекстЗапроса));

	Запрос.УстановитьПараметр("МаркаНаименование", Марка);

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("МодельНаименованиеСсылка");

КонецФункции

// Обновляет справочник данными из файла поставщика
// 
// Параметры:
// 	ИмяФайла - Строка - Полное имя файла
// Возвращаемое значение:
// 	Структура - Описание:
// * ИмяСправочника - Строка - Наименование справочника
// * Загружено - Число - Количество обновленных элементов справочника
// * Ошибок - Число - Количество ошибок обновления
Функция Обновить(ИмяФайла) Экспорт

	ИмяСправочника = Метаданные.Справочники.КА2_Модели.Имя;

	РезультатОбновления = КА2_КаталогАвтомобилейВызовСервера.ОбновитьСправочник(ИмяСправочника, ИмяФайла);

	Возврат РезультатОбновления;

КонецФункции

// Возвращает выборку элементов справочника с пустым полем date_create
// 
// Возвращаемое значение:
// 	ВыборкаИзРезультатаЗапроса - Описание
Функция Некорректные() Экспорт

	Возврат КА2_КаталогАвтомобилейВызовСервера.КоллекцияНекорректных(Метаданные.Справочники.КА2_Модели.Имя);

КонецФункции

//@skip-warning
Функция ДополнительныеДанные(Ссылка) Экспорт

КонецФункции

#КонецОбласти

#КонецЕсли