// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Марка = Неопределено;
	Модель = Неопределено;

	// Если в параметры переданы "Ссылка" с типом СправочникСсылка.КА2_НаименованияМарок и "Наименование" с типом
	// СправочникСсылка.КА2_НаименованияМоделей - получаем коллекцию поколений и устанавливаем отбор динамического списка
	Если Параметры.Отбор.Свойство("Ссылка", Марка)
		 И Параметры.Отбор.Свойство("Наименование", Модель)
		 И ТипЗнч(Марка) = Тип("СправочникСсылка.КА2_НаименованияМарок")
		 И ТипЗнч(Модель) = Тип("СправочникСсылка.КА2_НаименованияМоделей") Тогда

		НаборПоколений = Справочники.КА2_Поколения.НайтиПоНаименованиямМаркиИМодели(Марка, Модель);

		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																				"Ссылка",
																				НаборПоколений,
																				ВидСравненияКомпоновкиДанных.ВСписке);

		СтандартнаяОбработка = Ложь;

	КонецЕсли;

	// Кеширование текста запроса динамического списка
	КешТекстаЗапроса = Список.ТекстЗапроса;
	
	// Отбор только по записям из файлов последней поставки
	ТолькоИзБазыПоставщика = Истина;
	ТолькоИзБазыПоставщикаУстановитьОтбор();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТолькоИзБазыПоставщикаПриИзменении(Элемент)

	ТолькоИзБазыПоставщикаУстановитьОтбор();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличнойЧастиСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Поколения", Строки.ПолучитьКлючи());
	Запрос.Текст = "ВЫБРАТЬ
				   |	КА2_Поколения.Ссылка КАК Ссылка,
				   |	КОЛИЧЕСТВО(КА2_КлючиМоделей.Ссылка) КАК Вхождений
				   |ИЗ
				   |	Справочник.КА2_КлючиМоделей КАК КА2_КлючиМоделей
				   |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КА2_Поколения КАК КА2_Поколения
				   |		ПО КА2_КлючиМоделей.Поколение = КА2_Поколения.Ссылка
				   |ГДЕ
				   |	КА2_КлючиМоделей.Поколение В (&Поколения)
				   |СГРУППИРОВАТЬ ПО
				   |	КА2_Поколения.Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();

	КА2_КаталогАвтомобилейВызовСервера.УстановитьВыделениеСтрок(Строки, Выборка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТолькоИзБазыПоставщикаУстановитьОтбор()

	Если ТолькоИзБазыПоставщика Тогда
		КешТекстаЗапроса = Список.ТекстЗапроса;
		ЗапросДопОтбор = " И НЕ КА2_Поколения.ПометкаУдаления И КА2_Поколения.date_create <> """"";
		Список.ТекстЗапроса = КешТекстаЗапроса + ЗапросДопОтбор;
	Иначе
		Список.ТекстЗапроса = КешТекстаЗапроса;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

