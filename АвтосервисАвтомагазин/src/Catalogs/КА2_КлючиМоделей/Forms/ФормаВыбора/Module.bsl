// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Марка = Неопределено;
	Модель = Неопределено;

	// Если в параметры переданы "Ссылка" с типом СправочникСсылка.КА2_НаименованияМарок и "Наименование" с типом
	// СправочникСсылка.КА2_НаименованияМоделей - получаем коллекцию поколений и устанавливаем отбор динамического списка
	Если Параметры.Свойство("Марка", Марка)
		 И Параметры.Свойство("Модель", Модель)
		 И ТипЗнч(Марка) = Тип("СправочникСсылка.КА2_НаименованияМарок")
		 И ТипЗнч(Модель) = Тип("СправочникСсылка.КА2_НаименованияМоделей") Тогда

		НаборПоколений = Справочники.КА2_Поколения.НайтиПоНаименованиямМаркиИМодели(Марка, Модель);
		НаборКузовов = Справочники.КА2_Кузова.НайтиПоНаименованиямМаркиИМодели(Марка, Модель);

		ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.Отбор,
																				 "ПоколениеИлиКузов",
																				 ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
															   "Поколение",
															   ВидСравненияКомпоновкиДанных.ВСписке,
															   НаборПоколений,
															   ,
															   ЗначениеЗаполнено(НаборПоколений));
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
															   "Кузов",
															   ВидСравненияКомпоновкиДанных.ВСписке,
															   НаборКузовов,
															   ,
															   ЗначениеЗаполнено(НаборКузовов));

		СтандартнаяОбработка = Ложь;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	РезультатВыбора = Новый Структура("Марка, Модель, Поколение, Кузов, ПоколениеКузов");
	ЗаполнитьЗначенияСвойств(РезультатВыбора, Элемент.ТекущиеДанные);

	ОповеститьОВыборе(РезультатВыбора);

КонецПроцедуры

#КонецОбласти