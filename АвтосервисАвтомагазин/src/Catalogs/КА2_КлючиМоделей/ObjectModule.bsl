// 
//	Аутсорсинг Групп 
//		+7 495 241 10 64
//		+7 3852 59 50 96
//		
//	Филимонов И.В.
//		+7 913 240 81 77
//
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);

	Если ТипДанныхЗаполнения = Тип("Структура") Тогда

		ДанныеЗаполнения.Свойство("ТипТС", ТипТС);
		ДанныеЗаполнения.Свойство("Марка", Марка);
		ДанныеЗаполнения.Свойство("Модель", Модель);
		ДанныеЗаполнения.Свойство("Поколение", Поколение);
		ДанныеЗаполнения.Свойство("Кузов", Кузов);
		ДанныеЗаполнения.Свойство("Модификация", Модификация);
		ДанныеЗаполнения.Свойство("Комплектация", Комплектация);

	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДляНаименованияРазделитель = ", ";
	ДляПоколениеКузовРазделитель = " ";
	НеПустоеПоколениеКузов = Ложь;

	ДляНаименования = Новый Массив;
	ДляПоколениеКузов = Новый Массив;
		
	// Наименорвание марки
	ДляНаименования.Добавить(Строка(Марка));

	// Наименование модели
	Если ЗначениеЗаполнено(Модель) Тогда

		МодельНаименование = Строка(Модель);

		ДляНаименования.Добавить(ДляНаименованияРазделитель);
		ДляНаименования.Добавить(МодельНаименование);

		ДляПоколениеКузов.Добавить(МодельНаименование);
		НеПустоеПоколениеКузов = Истина;

	КонецЕсли;

	// Наименование поколения
	Если ЗначениеЗаполнено(Поколение) Тогда

		ПоколениеНаименование = Строка(Поколение);

		ДляНаименования.Добавить(ДляНаименованияРазделитель);
		ДляНаименования.Добавить(ПоколениеНаименование);

		Если НеПустоеПоколениеКузов Тогда
			ДляПоколениеКузов.Добавить(ДляПоколениеКузовРазделитель);
		Иначе
			НеПустоеПоколениеКузов = Истина;
		КонецЕсли;

		ГодыВыпускаСтруктура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Поколение, "year_begin, year_end");
		ГодыВыпуска = КА2_КаталогАвтомобилейКлиентСервер.ГодыВыпускаСтрока(ГодыВыпускаСтруктура.year_begin,
																		   ГодыВыпускаСтруктура.year_end);
		ДляПоколениеКузов.Добавить(ГодыВыпуска);

	КонецЕсли;

	// Наименование кузова
	Если ЗначениеЗаполнено(Кузов) Тогда
		КузовНаименование = Строка(Кузов);

		ДляНаименования.Добавить(ДляНаименованияРазделитель);
		ДляНаименования.Добавить(КузовНаименование);

		Если НеПустоеПоколениеКузов Тогда
			ДляПоколениеКузов.Добавить(ДляПоколениеКузовРазделитель);
		Иначе
			НеПустоеПоколениеКузов = Истина;
		КонецЕсли;

		ДляПоколениеКузов.Добавить(КузовНаименование);

	КонецЕсли;
			
	// Наименование модификации
	Если ЗначениеЗаполнено(Модификация) Тогда
		ДляНаименования.Добавить(ДляНаименованияРазделитель);
		ДляНаименования.Добавить(Строка(Модификация));
	КонецЕсли;
	
	// Наименование комплектации
	Если ЗначениеЗаполнено(Комплектация) Тогда
		ДляНаименования.Добавить(ДляНаименованияРазделитель);
		ДляНаименования.Добавить(Строка(Комплектация));
	КонецЕсли;

	Наименование = СтрСоединить(ДляНаименования);
	ПоколениеКузов = СтрСоединить(ДляПоколениеКузов);

КонецПроцедуры

#КонецОбласти

#КонецЕсли